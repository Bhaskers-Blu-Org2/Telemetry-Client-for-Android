apply plugin: 'java'
apply plugin: "com.jfrog.bintray"

sourceCompatibility = 1.6
targetCompatibility = 1.6

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir 'src/generated/java'
        }            
    }
    test {
        java {
            srcDir 'src/test/java'
            srcDir 'src/test/generated/java'
        }
    }
}

dependencies {
    compile 'org.json:json:20140107'
    compile project(':SharedTelemetryContracts')

    testCompile('junit:junit:4.12')
    testCompile('org.powermock:powermock-module-junit4:1.6.1')
    testCompile('org.powermock:powermock-api-easymock:1.6.1')
}

task docs(type: Javadoc) {
    source =sourceSets.main.java.srcDirs
    options.links("http://d.android.com/reference/")
}

task docsJar(type: Jar) {
    classifier = 'javadoc'
    baseName = project.name
    from docs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    baseName = project.name
    from sourceSets.main.java.srcDirs
}

artifacts {
    archives docsJar
    archives sourcesJar
}

status = 'Release'
group = projectGroup
version=projectVersion

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'jar'
                name projectName
                description projectDesc
                url gitUrl

                licenses {
                    license {
                        name 'MIT'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'asimovteam'
                        email 'reachcll@microsoft.com'
                    }
                }

                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url gitUrl
                }
            }
        }
    }
}

bintray {
    user = System.getenv("bintrayUser")
    key = System.getenv("bintrayKey")

    dryRun = false // Whether to run this as dry-run, without deploying
    publish = false // If version should be auto published after an upload

    configurations = ['archives']

    pkg {
        repo = 'maven'
        name = projectName
        desc = projectDesc
        websiteUrl = siteUrl
        issueTrackerUrl = siteUrl + '/issues'
        vcsUrl = gitUrl
        licenses = ['MIT']
        publicDownloadNumbers = true

        version {
            name = projectVersion
            vcsTag = projectVersion
            gpg {
                sign = true
                passphrase = System.getenv("bintrayGPGPassphrase")
            }
        }
    }
}