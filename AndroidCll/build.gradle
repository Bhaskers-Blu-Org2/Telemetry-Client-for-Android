apply plugin: 'com.android.library'
apply plugin: "com.jfrog.bintray"
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 22
    buildToolsVersion '21.1.2'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 22
        versionCode 1
        versionName projectVersion
    }
}

android {
    sourceSets {
        main {
            java {
                srcDir 'src/main/java'                                        
            }
            res {
                srcDir 'src/main/res'
            }
        }    
        androidTest {
            java {
                srcDir 'src/androidTest/java'
            }
        }    
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

task generateJar(type: Copy) {
    group 'Build'    
    dependsOn assemble
    from 'build/intermediates/bundles/release/classes.jar'
    into 'build/libs'
    rename('classes.jar', 'AndroidCll.jar')
}

build.finalizedBy generateJar

dependencies {
    compile project(':AndroidCore')
    testCompile('junit:junit:4.12')
}

task docs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    options.links("http://d.android.com/reference/")
}

task docsJar(type: Jar) {
    classifier = 'javadoc'
    baseName = project.name
    from docs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    baseName = project.name
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives docsJar
    archives sourcesJar
}

status='Release'
version=projectVersion
group=projectGroup

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name projectName
                description projectDesc
                url gitUrl

                licenses {
                    license {
                        name 'MIT'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'asimovteam'
                        email 'reachcll@microsoft.com'
                    }
                }

                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url gitUrl
                }
            }
        }
    }
}

bintray {
    user = System.getenv("bintrayUser")
    key = System.getenv("bintrayKey")

    dryRun = false // Whether to run this as dry-run, without deploying
    publish = false // If version should be auto published after an upload

    configurations = ['archives']

    pkg {
        repo = 'maven'
        name = projectName
        desc = projectDesc
        websiteUrl = siteUrl
        issueTrackerUrl = siteUrl + '/issues'
        vcsUrl = gitUrl
        licenses = ['MIT']
        publicDownloadNumbers = true

        version {
            name = projectVersion
            vcsTag = projectVersion
            gpg {
                sign = true
                passphrase = System.getenv("bintrayGPGPassphrase")
            }
        }
    }
}